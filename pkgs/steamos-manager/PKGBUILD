# Maintainer: Vicki Pfau <vi@endrift.com>
# Maintainer: Jeremy Whiting <jeremy.whiting@collabora.com>

pkgname=steamos-manager
_srctag=v25.4.1.sk1
pkgver=${_srctag##v}
pkgrel=0
pkgdesc='SteamOS Manager daemon for running various system management tasks'
arch=('x86_64')
url='https://store.steampowered.com/steamos/'
license=('MIT')
depends=('dbus'
         'systemd'
         'wireless_tools')  # For iwconfig
optdepends=('jupiter-hw-support: jupiter support'  # Needed for jupiter-get-als-gain, jupiter-biosupdate, steamos-format-device, steamos-trim-devices
            'jupiter-dock-updater-bin: jupiter dock updater'  # Needed for jupiter-dock-updater
            'steamos-customizations-jupiter: jupiter support'  # Needed for steamos-factory-reset-config
            'steamos-log-submitter: ftrace logging'
            'plasma-remotecontrollers: CEC wake support'
            'wakehook: CEC wake support')
makedepends=('git' 'cargo')
# _commit=1e6e80cf93629d9a5b5fd38c05bb7532a37510d1
source=("$pkgname-$pkgver::git+https://github.com/honjow/$pkgname.git#tag=${_srctag}")
sha256sums=('SKIP')
replaces=('ds-inhibit')

prepare() {
	export RUSTUP_TOOLCHAIN=stable
}

build() {
	cd "$pkgname-$pkgver"
	cargo build -r
}

package () {
	cd "$pkgname-$pkgver"
	install -d -m0755 "$pkgdir/usr/share/dbus-1/services/"
	install -d -m0755 "$pkgdir/usr/share/dbus-1/system-services/"
	install -d -m0755 "$pkgdir/usr/share/dbus-1/system.d/"
	install -d -m0755 "$pkgdir/usr/lib/systemd/system/"
	install -d -m0755 "$pkgdir/usr/lib/systemd/user/gamescope-session.service.wants/"

	install -Ds -m755 "target/release/steamos-manager" "$pkgdir/usr/lib/steamos-manager"
	install -D -m755 "target/release/steamosctl" "$pkgdir/usr/bin/steamosctl"
	install -D -m644 -t "$pkgdir/usr/share/steamos-manager/platforms" "data/platforms/"*
	install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname"

	install -m644 "data/system/com.steampowered.SteamOSManager1.service" "$pkgdir/usr/share/dbus-1/system-services/"
	install -m644 "data/system/com.steampowered.SteamOSManager1.conf" "$pkgdir/usr/share/dbus-1/system.d/"
	install -m644 "data/system/steamos-manager.service" "$pkgdir/usr/lib/systemd/system/"

	install -m644 "data/user/com.steampowered.SteamOSManager1.service" "$pkgdir/usr/share/dbus-1/services/"
	install -m644 "data/user/steamos-manager.service" "$pkgdir/usr/lib/systemd/user/"

	ln -s ../steamos-manager.service "$pkgdir/usr/lib/systemd/user/gamescope-session.service.wants/"
}

# check() {
# 	cd "$pkgname-$pkgver"
# 	dbus-run-session cargo test
# }
