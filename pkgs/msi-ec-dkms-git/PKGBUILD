# Maintainer: steelt <steeltitanium1 at gmail dot com>

_pkgbase=msi-ec
pkgname=msi-ec-dkms-git
pkgver=r415.f169e6a
pkgrel=3
pkgdesc="Driver for MSI laptop EC (DKMS)"
arch=('x86_64')
license=('GPL2')
url="https://github.com/BeardOverflow/msi-ec"
depends=('dkms')
makedepends=('git')
conflicts=("${_pkgbase}")
source=(
    "git+https://github.com/BeardOverflow/msi-ec.git"
    "dkms.conf"
    "0001-Add-Claw-8.patch"
    "0002-Implement-Fan-Thermal-Curve.patch"
    "0003-Add-fan_curve-for-claw8.patch"
    "0004-Add-hwmon-support-for-fan-speed-and-temperature-moni.patch"
    "0005-Add-fan-label-support-to-hwmon-interface.patch"
    "0006-hwmon-Implement-fan-PWM-control-functionality.patch"
    )
sha256sums=('SKIP'
            'f2846d9e9b3734dbdeb41dafde34ec5b5fd2353732cb30cf2606ba89083e1875'
            'f0ea2bda97f7dc775d0d2849ee6aa8385eb42f7f66d6c99a0cc0215177e3ab92'
            '3c6c47e04e1251f03996114ce4f61aaddf1e2617a34cc77d676ac57cf13918f0'
            'ec88b925323fe2cfe6cd2717756882df1be6b68559c39b2e313d9e9f49b51b77'
            '8566ca27181cc5013f39e62bd3211e8282b9632ec1dade19699d30977f741837'
            '0a82a9ff432eba9b699f1736696a3953464e00e7116ba0ca9bd90d9f7630321c'
            'efa5cac78de69d7e938d4294c696ff6997bc491d5546af6ca17f628e00165151')

pkgver() {
    cd ${_pkgbase}
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd ${_pkgbase}

    local src
    for src in "${source[@]}"; do
        [[ $src = *.patch ]] || continue
        patch -p1 < "${srcdir}"/$src
    done
}

package() {
  install -Dm644 dkms.conf "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/dkms.conf

  sed -e "s/@VERSION@/${pkgver}/" \
      -i "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/dkms.conf 

  cd "${_pkgbase}"

  install -Dm644 -t "${pkgdir}/usr/src/${_pkgbase}-${pkgver}" \
    Makefile \
    msi-ec.c \
    ec_memory_configuration.h
}
