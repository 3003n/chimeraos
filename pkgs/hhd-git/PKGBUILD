# Maintainer: Antheas Kapenekakis <aur at antheas dot dev>
pkgname=hhd-git
_pkgname=hhd
pkgver=v1.1.0.6.g9f09d7b
pkgrel=1
pkgdesc='Handheld Daemon. A tool for managing the quirks of handheld devices.'
arch=('x86_64')
url='https://github.com/hhd-dev/hhd'
license=('MIT')
depends=('python' python-setuptools 'python-evdev' 'python-rich' 'python-yaml')
provides=('hhd')
conflicts=('hhd')
optdepends=('hhd-user: allows running hhd as a user service.')
makedepends=('python-'{'build','installer','setuptools','wheel'})
source=("git+https://github.com/hhd-dev/hhd.git")
sha512sums=('SKIP')

prepare() {
  # patch
  cd "$srcdir/$_pkgname"

  echo "Preparing..."

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done

}

build() {
  cd "$srcdir/$_pkgname"

  sed -i "s/^# gpd_win = /gpd_win = /" "pyproject.toml"
  sed -i "s/get_outputs_config(can_disable=False)/get_outputs_config(can_disable=True)/" "src/hhd/device/gpd/win/__init__.py"

  python -m build --wheel --no-isolation
}

pkgver() {
  cd "$srcdir/$_pkgname"
  # printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  git describe --tags | sed 's/\-/\./g'
}

package() {
  cd "$srcdir/$_pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
  
  # Install minimally necessary rules for running as a system service
  mkdir -p ${pkgdir}/usr/lib/udev/rules.d/
  install -m644 usr/lib/udev/rules.d/83-hhd.rules ${pkgdir}/usr/lib/udev/rules.d/83-hhd.rules
  mkdir -p ${pkgdir}/usr/lib/systemd/system/
  install -m644 usr/lib/systemd/system/hhd@.service ${pkgdir}/usr/lib/systemd/system/hhd@.service
}
