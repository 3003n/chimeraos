# Maintainer: Danct12 <danct12@disroot.org>

pkgname=libgbinder
pkgver=1.1.42
pkgrel=1
pkgdesc="GLib-style interface to binder"
arch=('x86_64' 'i686' 'armv7h' 'aarch64')
url="https://github.com/mer-hybris/libgbinder.git"
license=('BSD-3-Clause')
depends=('libglibutil' 'glib2')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mer-hybris/libgbinder/archive/refs/tags/${pkgver}.tar.gz"
        "0001-fix-for-gcc15.patch"
        )
sha512sums=('4d229e957188064817d6b40f693cf6fe9b44efae34ad15ac4314c7f0e3c49dc17835984fa741f7b90d8a2016234f2e7e78cc56f73625156e2aedab73faa93553'
            'f488664537ce0bc03b2b5b29d8e1666bb516f6e06280553cc7949e674acea7020405cb0d52bfb89359dcdacf13dc1768ca2e6a4dcca29b1b5b9d467f713b5a6c')

prepare() {
  cd $pkgname-$pkgver

  local src
  for src in "${source[@]}"; do
    [[ $src = *.patch ]] || continue
    patch -p1 < "${srcdir}"/$src
  done
}

build() {
  cd $pkgname-$pkgver
  make KEEP_SYMBOLS=1 release pkgconfig
}

package() {
  cd $pkgname-$pkgver
  make install-dev DESTDIR="${pkgdir}"
}