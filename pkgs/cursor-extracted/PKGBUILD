pkgname=cursor-extracted
pkgver=1.5.1
pkgrel=1
pkgdesc='Cursor - The AI Code Editor (extracted from Appimage)'
arch=('x86_64')
provides=('cursor')
conflicts=('cursor' 'cursor-bin')
url='https://www.cursor.com/'
license=('custom:Proprietary')
source=('cursor-download.sh')
sha512sums=('SKIP')
options=('!strip' '!debug')

pkgver() {
	# cd "${srcdir}/sk-chos-scripts"
	./cursor-download.sh version
}

prepare() {
	# cd "${srcdir}/sk-chos-scripts"
	# cp cursor-download.sh "${srcdir}"
	version=$(./cursor-download.sh version)
	echo "version: ${version}"
	cd "${srcdir}"
	./cursor-download.sh download
	chmod +x "${srcdir}/cursor-${version}.Appimage"
	"${srcdir}/cursor-${version}.Appimage" --appimage-extract >/dev/null
	sed 's@AppRun@/usr/bin/cursor@g' -i "${srcdir}/squashfs-root/cursor.desktop"
	sed 's@co.anysphere.cursor@cursor@g' -i "${srcdir}/squashfs-root/cursor.desktop" 
}

package() {
	install -Dm644 "${srcdir}/squashfs-root/cursor.desktop" "${pkgdir}/usr/share/applications/cursor.desktop"

	find "${srcdir}/squashfs-root" -type f |
		sed "s@^${srcdir}/squashfs-root/@@g" |
		xargs -I{} install -D "${srcdir}/squashfs-root/{}" "${pkgdir}/opt/cursor/{}"

	mkdir -p "${pkgdir}/usr/bin"
	ln -sf "/opt/cursor/usr/share/cursor/cursor" "${pkgdir}/usr/bin/cursor"

	find "${srcdir}/squashfs-root/usr/share/icons/hicolor" -type f -name 'cursor.png' |
		sed "s@^${srcdir}/squashfs-root/@@g" |
		xargs -I{} install -Dm644 "${srcdir}/squashfs-root/{}" "${pkgdir}/{}"
}
